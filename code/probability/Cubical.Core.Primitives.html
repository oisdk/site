<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Core.Primitives</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-

This file document and export the main primitives of Cubical Agda. It
also defines some basic derived operations (composition and filling).

-}</a>
<a id="149" class="Symbol">{-#</a> <a id="153" class="Keyword">OPTIONS</a> <a id="161" class="Pragma">--cubical</a> <a id="171" class="Pragma">--safe</a> <a id="178" class="Symbol">#-}</a>
<a id="182" class="Keyword">module</a> <a id="189" href="Cubical.Core.Primitives.html" class="Module">Cubical.Core.Primitives</a> <a id="213" class="Keyword">where</a>

<a id="220" class="Keyword">open</a> <a id="225" class="Keyword">import</a> <a id="232" href="Agda.Builtin.Cubical.Path.html" class="Module">Agda.Builtin.Cubical.Path</a> <a id="258" class="Keyword">public</a>
<a id="265" class="Keyword">open</a> <a id="270" class="Keyword">import</a> <a id="277" href="Agda.Builtin.Cubical.Sub.html" class="Module">Agda.Builtin.Cubical.Sub</a> <a id="302" class="Keyword">public</a>
  <a id="311" class="Keyword">renaming</a> <a id="320" class="Symbol">(</a> <a id="322" href="Agda.Builtin.Cubical.Sub.html#188" class="Postulate">inc</a> <a id="326" class="Symbol">to</a> <a id="329" href="Agda.Builtin.Cubical.Sub.html#188" class="Postulate">inS</a>
           <a id="344" class="Symbol">;</a> <a id="346" href="Agda.Builtin.Cubical.Sub.html#290" class="Primitive">primSubOut</a> <a id="357" class="Symbol">to</a> <a id="360" href="Agda.Builtin.Cubical.Sub.html#290" class="Primitive">outS</a>
           <a id="376" class="Symbol">)</a>
<a id="378" class="Keyword">open</a> <a id="383" class="Keyword">import</a> <a id="390" href="Agda.Primitive.Cubical.html" class="Module">Agda.Primitive.Cubical</a> <a id="413" class="Keyword">public</a>
  <a id="422" class="Keyword">renaming</a> <a id="431" class="Symbol">(</a> <a id="433" href="Agda.Primitive.Cubical.html#226" class="Primitive">primIMin</a>       <a id="448" class="Symbol">to</a> <a id="451" href="Agda.Primitive.Cubical.html#226" class="Primitive">_∧_</a>  <a id="456" class="Comment">-- I → I → I</a>
           <a id="480" class="Symbol">;</a> <a id="482" href="Agda.Primitive.Cubical.html#251" class="Primitive">primIMax</a>       <a id="497" class="Symbol">to</a> <a id="500" href="Agda.Primitive.Cubical.html#251" class="Primitive">_∨_</a>  <a id="505" class="Comment">-- I → I → I</a>
           <a id="529" class="Symbol">;</a> <a id="531" href="Agda.Primitive.Cubical.html#276" class="Primitive">primINeg</a>       <a id="546" class="Symbol">to</a> <a id="549" href="Agda.Primitive.Cubical.html#276" class="Primitive">~_</a>   <a id="554" class="Comment">-- I → I</a>
           <a id="574" class="Symbol">;</a> <a id="576" href="Agda.Primitive.Cubical.html#744" class="Postulate">isOneEmpty</a>     <a id="591" class="Symbol">to</a> <a id="594" href="Agda.Primitive.Cubical.html#744" class="Postulate">empty</a>
           <a id="611" class="Symbol">;</a> <a id="613" href="Agda.Primitive.Cubical.html#1125" class="Primitive">primComp</a>       <a id="628" class="Symbol">to</a> <a id="631" href="Agda.Primitive.Cubical.html#1125" class="Primitive">compCCHM</a>  <a id="641" class="Comment">-- This should not be used</a>
           <a id="679" class="Symbol">;</a> <a id="681" href="Agda.Primitive.Cubical.html#1352" class="Primitive">primHComp</a>      <a id="696" class="Symbol">to</a> <a id="699" href="Agda.Primitive.Cubical.html#1352" class="Primitive">hcomp</a>
           <a id="716" class="Symbol">;</a> <a id="718" href="Agda.Primitive.Cubical.html#1279" class="Primitive">primTransp</a>     <a id="733" class="Symbol">to</a> <a id="736" href="Agda.Primitive.Cubical.html#1279" class="Primitive">transp</a>
           <a id="754" class="Symbol">;</a> <a id="756" href="Agda.Primitive.Cubical.html#362" class="Postulate">itIsOne</a>        <a id="771" class="Symbol">to</a> <a id="774" href="Agda.Primitive.Cubical.html#362" class="Postulate">1=1</a> <a id="778" class="Symbol">)</a>
<a id="780" class="Keyword">open</a> <a id="785" class="Keyword">import</a> <a id="792" href="Agda.Primitive.html" class="Module">Agda.Primitive</a> <a id="807" class="Keyword">public</a>
  <a id="816" class="Keyword">using</a>    <a id="825" class="Symbol">(</a> <a id="827" href="Agda.Primitive.html#408" class="Postulate">Level</a> <a id="833" class="Symbol">)</a>
  <a id="837" class="Keyword">renaming</a> <a id="846" class="Symbol">(</a> <a id="848" href="Agda.Primitive.html#611" class="Primitive">lzero</a> <a id="854" class="Symbol">to</a> <a id="857" href="Agda.Primitive.html#611" class="Primitive">ℓ-zero</a>
           <a id="875" class="Symbol">;</a> <a id="877" href="Agda.Primitive.html#627" class="Primitive">lsuc</a>  <a id="883" class="Symbol">to</a> <a id="886" href="Agda.Primitive.html#627" class="Primitive">ℓ-suc</a>
           <a id="903" class="Symbol">;</a> <a id="905" href="Agda.Primitive.html#657" class="Primitive Operator">_⊔_</a>   <a id="911" class="Symbol">to</a> <a id="914" href="Agda.Primitive.html#657" class="Primitive Operator">ℓ-max</a> <a id="920" class="Symbol">)</a>
<a id="922" class="Keyword">open</a> <a id="927" class="Keyword">import</a> <a id="934" href="Agda.Builtin.Sigma.html" class="Module">Agda.Builtin.Sigma</a> <a id="953" class="Keyword">public</a>

<a id="961" class="Comment">-- This file document the Cubical Agda primitives. The primitives</a>
<a id="1027" class="Comment">-- themselves are bound by the Agda files imported above.</a>

<a id="1086" class="Comment">-- * The Interval</a>
<a id="1104" class="Comment">-- I : Setω</a>

<a id="1117" class="Comment">-- Endpoints, Connections, Reversal</a>
<a id="1153" class="Comment">-- i0 i1   : I</a>
<a id="1168" class="Comment">-- _∧_ _∨_ : I → I → I</a>
<a id="1191" class="Comment">-- ~_      : I → I</a>


<a id="1212" class="Comment">-- * Dependent path type. (Path over Path)</a>

<a id="1256" class="Comment">-- Introduced with lambda abstraction and eliminated with application,</a>
<a id="1327" class="Comment">-- just like function types.</a>

<a id="1357" class="Comment">-- PathP : ∀ {ℓ} (A : I → Set ℓ) → A i0 → A i1 → Set ℓ</a>

<a id="1413" class="Keyword">infix</a> <a id="1419" class="Number">4</a> <a id="1421" href="Cubical.Core.Primitives.html#1429" class="Function Operator">_[_≡_]</a>

<a id="_[_≡_]"></a><a id="1429" href="Cubical.Core.Primitives.html#1429" class="Function Operator">_[_≡_]</a> <a id="1436" class="Symbol">:</a> <a id="1438" class="Symbol">∀</a> <a id="1440" class="Symbol">{</a><a id="1441" href="Cubical.Core.Primitives.html#1441" class="Bound">ℓ</a><a id="1442" class="Symbol">}</a> <a id="1444" class="Symbol">(</a><a id="1445" href="Cubical.Core.Primitives.html#1445" class="Bound">A</a> <a id="1447" class="Symbol">:</a> <a id="1449" href="Agda.Primitive.Cubical.html#86" class="Datatype">I</a> <a id="1451" class="Symbol">→</a> <a id="1453" class="PrimitiveType">Set</a> <a id="1457" href="Cubical.Core.Primitives.html#1441" class="Bound">ℓ</a><a id="1458" class="Symbol">)</a> <a id="1460" class="Symbol">→</a> <a id="1462" href="Cubical.Core.Primitives.html#1445" class="Bound">A</a> <a id="1464" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a> <a id="1467" class="Symbol">→</a> <a id="1469" href="Cubical.Core.Primitives.html#1445" class="Bound">A</a> <a id="1471" href="Agda.Primitive.Cubical.html#156" class="InductiveConstructor">i1</a> <a id="1474" class="Symbol">→</a> <a id="1476" class="PrimitiveType">Set</a> <a id="1480" href="Cubical.Core.Primitives.html#1441" class="Bound">ℓ</a>
<a id="1482" href="Cubical.Core.Primitives.html#1429" class="Function Operator">_[_≡_]</a> <a id="1489" class="Symbol">=</a> <a id="1491" href="Agda.Builtin.Cubical.Path.html#162" class="Postulate">PathP</a>


<a id="1499" class="Comment">-- Non dependent path types</a>

<a id="Path"></a><a id="1528" href="Cubical.Core.Primitives.html#1528" class="Function">Path</a> <a id="1533" class="Symbol">:</a> <a id="1535" class="Symbol">∀</a> <a id="1537" class="Symbol">{</a><a id="1538" href="Cubical.Core.Primitives.html#1538" class="Bound">ℓ</a><a id="1539" class="Symbol">}</a> <a id="1541" class="Symbol">(</a><a id="1542" href="Cubical.Core.Primitives.html#1542" class="Bound">A</a> <a id="1544" class="Symbol">:</a> <a id="1546" class="PrimitiveType">Set</a> <a id="1550" href="Cubical.Core.Primitives.html#1538" class="Bound">ℓ</a><a id="1551" class="Symbol">)</a> <a id="1553" class="Symbol">→</a> <a id="1555" href="Cubical.Core.Primitives.html#1542" class="Bound">A</a> <a id="1557" class="Symbol">→</a> <a id="1559" href="Cubical.Core.Primitives.html#1542" class="Bound">A</a> <a id="1561" class="Symbol">→</a> <a id="1563" class="PrimitiveType">Set</a> <a id="1567" href="Cubical.Core.Primitives.html#1538" class="Bound">ℓ</a>
<a id="1569" href="Cubical.Core.Primitives.html#1528" class="Function">Path</a> <a id="1574" href="Cubical.Core.Primitives.html#1574" class="Bound">A</a> <a id="1576" href="Cubical.Core.Primitives.html#1576" class="Bound">a</a> <a id="1578" href="Cubical.Core.Primitives.html#1578" class="Bound">b</a> <a id="1580" class="Symbol">=</a> <a id="1582" href="Agda.Builtin.Cubical.Path.html#162" class="Postulate">PathP</a> <a id="1588" class="Symbol">(λ</a> <a id="1591" href="Cubical.Core.Primitives.html#1591" class="Bound">_</a> <a id="1593" class="Symbol">→</a> <a id="1595" href="Cubical.Core.Primitives.html#1574" class="Bound">A</a><a id="1596" class="Symbol">)</a> <a id="1598" href="Cubical.Core.Primitives.html#1576" class="Bound">a</a> <a id="1600" href="Cubical.Core.Primitives.html#1578" class="Bound">b</a>

<a id="1603" class="Comment">-- PathP (λ i → A) x y gets printed as x ≡ y when A does not mention i.</a>
<a id="1675" class="Comment">--  _≡_ : ∀ {ℓ} {A : Set ℓ} → A → A → Set ℓ</a>
<a id="1719" class="Comment">--  _≡_ {A = A} = PathP (λ _ → A)</a>


<a id="1755" class="Comment">-- * @IsOne r@ represents the constraint &quot;r = i1&quot;.</a>
<a id="1806" class="Comment">-- Often we will use &quot;φ&quot; for elements of I, when we intend to use them</a>
<a id="1877" class="Comment">-- with IsOne (or Partial[P]).</a>
<a id="1908" class="Comment">-- IsOne : I → Setω</a>

<a id="1929" class="Comment">-- i1 is indeed equal to i1.</a>
<a id="1958" class="Comment">-- 1=1 : IsOne i1</a>


<a id="1978" class="Comment">-- * Types of partial elements, and their dependent version.</a>

<a id="2040" class="Comment">-- &quot;Partial φ A&quot; is a special version of &quot;IsOne φ → A&quot; with a more</a>
<a id="2107" class="Comment">-- extensional judgmental equality.</a>
<a id="2143" class="Comment">-- &quot;PartialP φ A&quot; allows &quot;A&quot; to be defined only on &quot;φ&quot;.</a>

<a id="2200" class="Comment">-- Partial : ∀ {ℓ} → I → Set ℓ → Setω</a>
<a id="2238" class="Comment">-- PartialP : ∀ {ℓ} → (φ : I) → Partial φ (Set ℓ) → Setω</a>

<a id="2296" class="Comment">-- Partial elements are introduced by pattern matching with (r = i0)</a>
<a id="2365" class="Comment">-- or (r = i1) constraints, like so:</a>

<a id="2403" class="Keyword">private</a>
  <a id="sys"></a><a id="2413" href="Cubical.Core.Primitives.html#2413" class="Function">sys</a> <a id="2417" class="Symbol">:</a> <a id="2419" class="Symbol">∀</a> <a id="2421" href="Cubical.Core.Primitives.html#2421" class="Bound">i</a> <a id="2423" class="Symbol">→</a> <a id="2425" href="Agda.Primitive.Cubical.html#684" class="Primitive">Partial</a> <a id="2433" class="Symbol">(</a><a id="2434" href="Cubical.Core.Primitives.html#2421" class="Bound">i</a> <a id="2436" href="Agda.Primitive.Cubical.html#251" class="Primitive Operator">∨</a> <a id="2438" href="Agda.Primitive.Cubical.html#276" class="Primitive Operator">~</a> <a id="2440" href="Cubical.Core.Primitives.html#2421" class="Bound">i</a><a id="2441" class="Symbol">)</a> <a id="2443" class="PrimitiveType">Set₁</a>
  <a id="2450" href="Cubical.Core.Primitives.html#2413" class="Function">sys</a> <a id="2454" href="Cubical.Core.Primitives.html#2454" class="Bound">i</a> <a id="2456" class="Symbol">(</a><a id="2457" href="Cubical.Core.Primitives.html#2454" class="Bound">i</a> <a id="2459" class="Symbol">=</a> <a id="2461" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a><a id="2463" class="Symbol">)</a> <a id="2465" class="Symbol">=</a> <a id="2467" class="PrimitiveType">Set</a>
  <a id="2473" href="Cubical.Core.Primitives.html#2413" class="Function">sys</a> <a id="2477" href="Cubical.Core.Primitives.html#2477" class="Bound">i</a> <a id="2479" class="Symbol">(</a><a id="2480" href="Cubical.Core.Primitives.html#2477" class="Bound">i</a> <a id="2482" class="Symbol">=</a> <a id="2484" href="Agda.Primitive.Cubical.html#156" class="InductiveConstructor">i1</a><a id="2486" class="Symbol">)</a> <a id="2488" class="Symbol">=</a> <a id="2490" class="PrimitiveType">Set</a> <a id="2494" class="Symbol">→</a> <a id="2496" class="PrimitiveType">Set</a>

  <a id="2503" class="Comment">-- It also works with pattern matching lambdas:</a>
  <a id="2553" class="Comment">--  http://wiki.portal.chalmers.se/agda/pmwiki.php?n=ReferenceManual.PatternMatchingLambdas</a>
  <a id="sys'"></a><a id="2647" href="Cubical.Core.Primitives.html#2647" class="Function">sys'</a> <a id="2652" class="Symbol">:</a> <a id="2654" class="Symbol">∀</a> <a id="2656" href="Cubical.Core.Primitives.html#2656" class="Bound">i</a> <a id="2658" class="Symbol">→</a> <a id="2660" href="Agda.Primitive.Cubical.html#684" class="Primitive">Partial</a> <a id="2668" class="Symbol">(</a><a id="2669" href="Cubical.Core.Primitives.html#2656" class="Bound">i</a> <a id="2671" href="Agda.Primitive.Cubical.html#251" class="Primitive Operator">∨</a> <a id="2673" href="Agda.Primitive.Cubical.html#276" class="Primitive Operator">~</a> <a id="2675" href="Cubical.Core.Primitives.html#2656" class="Bound">i</a><a id="2676" class="Symbol">)</a> <a id="2678" class="PrimitiveType">Set₁</a>
  <a id="2685" href="Cubical.Core.Primitives.html#2647" class="Function">sys'</a> <a id="2690" href="Cubical.Core.Primitives.html#2690" class="Bound">i</a> <a id="2692" class="Symbol">=</a> <a id="2694" class="Symbol">λ</a> <a id="2696" class="Symbol">{</a> <a id="2698" class="Symbol">(</a><a id="2699" href="Cubical.Core.Primitives.html#2690" class="Bound">i</a> <a id="2701" class="Symbol">=</a> <a id="2703" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a><a id="2705" class="Symbol">)</a> <a id="2707" class="Symbol">→</a> <a id="2709" class="PrimitiveType">Set</a>
             <a id="2726" class="Symbol">;</a> <a id="2728" class="Symbol">(</a><a id="2729" href="Cubical.Core.Primitives.html#2690" class="Bound">i</a> <a id="2731" class="Symbol">=</a> <a id="2733" href="Agda.Primitive.Cubical.html#156" class="InductiveConstructor">i1</a><a id="2735" class="Symbol">)</a> <a id="2737" class="Symbol">→</a> <a id="2739" class="PrimitiveType">Set</a> <a id="2743" class="Symbol">→</a> <a id="2745" class="PrimitiveType">Set</a>
             <a id="2762" class="Symbol">}</a>

  <a id="2767" class="Comment">-- When the cases overlap they must agree.</a>
  <a id="sys2"></a><a id="2812" href="Cubical.Core.Primitives.html#2812" class="Function">sys2</a> <a id="2817" class="Symbol">:</a> <a id="2819" class="Symbol">∀</a> <a id="2821" href="Cubical.Core.Primitives.html#2821" class="Bound">i</a> <a id="2823" href="Cubical.Core.Primitives.html#2823" class="Bound">j</a> <a id="2825" class="Symbol">→</a> <a id="2827" href="Agda.Primitive.Cubical.html#684" class="Primitive">Partial</a> <a id="2835" class="Symbol">(</a><a id="2836" href="Cubical.Core.Primitives.html#2821" class="Bound">i</a> <a id="2838" href="Agda.Primitive.Cubical.html#251" class="Primitive Operator">∨</a> <a id="2840" class="Symbol">(</a><a id="2841" href="Cubical.Core.Primitives.html#2821" class="Bound">i</a> <a id="2843" href="Agda.Primitive.Cubical.html#226" class="Primitive Operator">∧</a> <a id="2845" href="Cubical.Core.Primitives.html#2823" class="Bound">j</a><a id="2846" class="Symbol">))</a> <a id="2849" class="PrimitiveType">Set₁</a>
  <a id="2856" href="Cubical.Core.Primitives.html#2812" class="Function">sys2</a> <a id="2861" href="Cubical.Core.Primitives.html#2861" class="Bound">i</a> <a id="2863" href="Cubical.Core.Primitives.html#2863" class="Bound">j</a> <a id="2865" class="Symbol">=</a> <a id="2867" class="Symbol">λ</a> <a id="2869" class="Symbol">{</a> <a id="2871" class="Symbol">(</a><a id="2872" href="Cubical.Core.Primitives.html#2861" class="Bound">i</a> <a id="2874" class="Symbol">=</a> <a id="2876" href="Agda.Primitive.Cubical.html#156" class="InductiveConstructor">i1</a><a id="2878" class="Symbol">)</a>          <a id="2889" class="Symbol">→</a> <a id="2891" class="PrimitiveType">Set</a>
               <a id="2910" class="Symbol">;</a> <a id="2912" class="Symbol">(</a><a id="2913" href="Cubical.Core.Primitives.html#2861" class="Bound">i</a> <a id="2915" class="Symbol">=</a> <a id="2917" href="Agda.Primitive.Cubical.html#156" class="InductiveConstructor">i1</a><a id="2919" class="Symbol">)</a> <a id="2921" class="Symbol">(</a><a id="2922" href="Cubical.Core.Primitives.html#2863" class="Bound">j</a> <a id="2924" class="Symbol">=</a> <a id="2926" href="Agda.Primitive.Cubical.html#156" class="InductiveConstructor">i1</a><a id="2928" class="Symbol">)</a> <a id="2930" class="Symbol">→</a> <a id="2932" class="PrimitiveType">Set</a>
               <a id="2951" class="Symbol">}</a>

  <a id="2956" class="Comment">-- (i0 = i1) is actually absurd.</a>
  <a id="sys3"></a><a id="2991" href="Cubical.Core.Primitives.html#2991" class="Function">sys3</a> <a id="2996" class="Symbol">:</a> <a id="2998" href="Agda.Primitive.Cubical.html#684" class="Primitive">Partial</a> <a id="3006" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a> <a id="3009" class="PrimitiveType">Set₁</a>
  <a id="3016" href="Cubical.Core.Primitives.html#2991" class="Function">sys3</a> <a id="3021" class="Symbol">=</a> <a id="3023" class="Symbol">λ</a> <a id="3025" class="Symbol">{</a> <a id="3027" class="Symbol">()</a> <a id="3030" class="Symbol">}</a>


<a id="3034" class="Comment">-- * There are cubical subtypes as in CCHM. Note that these are not</a>
<a id="3102" class="Comment">-- fibrant (hence in Setω):</a>

<a id="_[_↦_]"></a><a id="3131" href="Cubical.Core.Primitives.html#3131" class="Function Operator">_[_↦_]</a> <a id="3138" class="Symbol">:</a> <a id="3140" class="Symbol">∀</a> <a id="3142" class="Symbol">{</a><a id="3143" href="Cubical.Core.Primitives.html#3143" class="Bound">ℓ</a><a id="3144" class="Symbol">}</a> <a id="3146" class="Symbol">(</a><a id="3147" href="Cubical.Core.Primitives.html#3147" class="Bound">A</a> <a id="3149" class="Symbol">:</a> <a id="3151" class="PrimitiveType">Set</a> <a id="3155" href="Cubical.Core.Primitives.html#3143" class="Bound">ℓ</a><a id="3156" class="Symbol">)</a> <a id="3158" class="Symbol">(</a><a id="3159" href="Cubical.Core.Primitives.html#3159" class="Bound">φ</a> <a id="3161" class="Symbol">:</a> <a id="3163" href="Agda.Primitive.Cubical.html#86" class="Datatype">I</a><a id="3164" class="Symbol">)</a> <a id="3166" class="Symbol">(</a><a id="3167" href="Cubical.Core.Primitives.html#3167" class="Bound">u</a> <a id="3169" class="Symbol">:</a> <a id="3171" href="Agda.Primitive.Cubical.html#684" class="Primitive">Partial</a> <a id="3179" href="Cubical.Core.Primitives.html#3159" class="Bound">φ</a> <a id="3181" href="Cubical.Core.Primitives.html#3147" class="Bound">A</a><a id="3182" class="Symbol">)</a> <a id="3184" class="Symbol">→</a> <a id="3186" href="Agda.Primitive.html#808" class="Primitive">Agda.Primitive.Setω</a>
<a id="3206" href="Cubical.Core.Primitives.html#3206" class="Bound">A</a> <a id="3208" href="Cubical.Core.Primitives.html#3131" class="Function Operator">[</a> <a id="3210" href="Cubical.Core.Primitives.html#3210" class="Bound">φ</a> <a id="3212" href="Cubical.Core.Primitives.html#3131" class="Function Operator">↦</a> <a id="3214" href="Cubical.Core.Primitives.html#3214" class="Bound">u</a> <a id="3216" href="Cubical.Core.Primitives.html#3131" class="Function Operator">]</a> <a id="3218" class="Symbol">=</a> <a id="3220" href="Agda.Builtin.Cubical.Sub.html#163" class="Postulate">Sub</a> <a id="3224" href="Cubical.Core.Primitives.html#3206" class="Bound">A</a> <a id="3226" href="Cubical.Core.Primitives.html#3210" class="Bound">φ</a> <a id="3228" href="Cubical.Core.Primitives.html#3214" class="Bound">u</a>

<a id="3231" class="Keyword">infix</a> <a id="3237" class="Number">4</a> <a id="3239" href="Cubical.Core.Primitives.html#3131" class="Function Operator">_[_↦_]</a>

<a id="3247" class="Comment">-- Any element u : A can be seen as an element of A [ φ ↦ u ] which</a>
<a id="3315" class="Comment">-- agrees with u on φ:</a>

<a id="3339" class="Comment">-- inS : ∀ {ℓ} {A : Set ℓ} {φ} (u : A) → A [ φ ↦ (λ _ → u) ]</a>

<a id="3401" class="Comment">-- One can also forget that an element agrees with u on φ:</a>

<a id="3461" class="Comment">-- outS : ∀ {ℓ} {A : Set ℓ} {φ : I} {u : Partial φ A} → A [ φ ↦ u ] → A</a>


<a id="3535" class="Comment">-- * Composition operation according to [CCHM 18].</a>
<a id="3586" class="Comment">-- When calling &quot;comp A φ u a&quot; Agda makes sure that &quot;a&quot; agrees with &quot;u i0&quot; on &quot;φ&quot;.</a>
<a id="3669" class="Comment">-- compCCHM : ∀ {ℓ} (A : (i : I) → Set ℓ) (φ : I) (u : ∀ i → Partial φ (A i)) (a : A i0) → A i1</a>

<a id="3766" class="Comment">-- Note: this is not recommended to use, instead use the CHM</a>
<a id="3827" class="Comment">-- primitives! The reason is that these work with HITs and produce</a>
<a id="3894" class="Comment">-- fewer empty systems.</a>


<a id="3920" class="Comment">-- * Generalized transport and homogeneous composition [CHM 18].</a>

<a id="3986" class="Comment">-- When calling &quot;transp A φ a&quot; Agda makes sure that &quot;A&quot; is constant on &quot;φ&quot;.</a>
<a id="4062" class="Comment">-- transp : ∀ {ℓ} (A : I → Set ℓ) (φ : I) (a : A i0) → A i1</a>

<a id="4123" class="Comment">-- When calling &quot;hcomp A φ u a&quot; Agda makes sure that &quot;a&quot; agrees with &quot;u i0&quot; on &quot;φ&quot;.</a>
<a id="4207" class="Comment">-- hcomp : ∀ {ℓ} {A : Set ℓ} {φ : I} (u : I → Partial φ A) (a : A) → A</a>

<a id="4279" class="Keyword">private</a>
  <a id="4289" class="Keyword">variable</a>
    <a id="4302" href="Cubical.Core.Primitives.html#4302" class="Generalizable">ℓ</a>  <a id="4305" class="Symbol">:</a> <a id="4307" href="Agda.Primitive.html#408" class="Postulate">Level</a>
    <a id="4317" href="Cubical.Core.Primitives.html#4317" class="Generalizable">ℓ′</a> <a id="4320" class="Symbol">:</a> <a id="4322" href="Agda.Primitive.Cubical.html#86" class="Datatype">I</a> <a id="4324" class="Symbol">→</a> <a id="4326" href="Agda.Primitive.html#408" class="Postulate">Level</a>

<a id="4333" class="Comment">-- Homogeneous filling</a>
<a id="hfill"></a><a id="4356" href="Cubical.Core.Primitives.html#4356" class="Function">hfill</a> <a id="4362" class="Symbol">:</a> <a id="4364" class="Symbol">{</a><a id="4365" href="Cubical.Core.Primitives.html#4365" class="Bound">A</a> <a id="4367" class="Symbol">:</a> <a id="4369" class="PrimitiveType">Set</a> <a id="4373" href="Cubical.Core.Primitives.html#4302" class="Generalizable">ℓ</a><a id="4374" class="Symbol">}</a>
        <a id="4384" class="Symbol">{</a><a id="4385" href="Cubical.Core.Primitives.html#4385" class="Bound">φ</a> <a id="4387" class="Symbol">:</a> <a id="4389" href="Agda.Primitive.Cubical.html#86" class="Datatype">I</a><a id="4390" class="Symbol">}</a>
        <a id="4400" class="Symbol">(</a><a id="4401" href="Cubical.Core.Primitives.html#4401" class="Bound">u</a> <a id="4403" class="Symbol">:</a> <a id="4405" class="Symbol">∀</a> <a id="4407" href="Cubical.Core.Primitives.html#4407" class="Bound">i</a> <a id="4409" class="Symbol">→</a> <a id="4411" href="Agda.Primitive.Cubical.html#684" class="Primitive">Partial</a> <a id="4419" href="Cubical.Core.Primitives.html#4385" class="Bound">φ</a> <a id="4421" href="Cubical.Core.Primitives.html#4365" class="Bound">A</a><a id="4422" class="Symbol">)</a>
        <a id="4432" class="Symbol">(</a><a id="4433" href="Cubical.Core.Primitives.html#4433" class="Bound">u0</a> <a id="4436" class="Symbol">:</a> <a id="4438" href="Cubical.Core.Primitives.html#4365" class="Bound">A</a> <a id="4440" href="Cubical.Core.Primitives.html#3131" class="Function Operator">[</a> <a id="4442" href="Cubical.Core.Primitives.html#4385" class="Bound">φ</a> <a id="4444" href="Cubical.Core.Primitives.html#3131" class="Function Operator">↦</a> <a id="4446" href="Cubical.Core.Primitives.html#4401" class="Bound">u</a> <a id="4448" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a> <a id="4451" href="Cubical.Core.Primitives.html#3131" class="Function Operator">]</a><a id="4452" class="Symbol">)</a>
        <a id="4462" class="Comment">-----------------------</a>
        <a id="4494" class="Symbol">(</a><a id="4495" href="Cubical.Core.Primitives.html#4495" class="Bound">i</a> <a id="4497" class="Symbol">:</a> <a id="4499" href="Agda.Primitive.Cubical.html#86" class="Datatype">I</a><a id="4500" class="Symbol">)</a> <a id="4502" class="Symbol">→</a> <a id="4504" href="Cubical.Core.Primitives.html#4365" class="Bound">A</a>
<a id="4506" href="Cubical.Core.Primitives.html#4356" class="Function">hfill</a> <a id="4512" class="Symbol">{</a><a id="4513" class="Argument">φ</a> <a id="4515" class="Symbol">=</a> <a id="4517" href="Cubical.Core.Primitives.html#4517" class="Bound">φ</a><a id="4518" class="Symbol">}</a> <a id="4520" href="Cubical.Core.Primitives.html#4520" class="Bound">u</a> <a id="4522" href="Cubical.Core.Primitives.html#4522" class="Bound">u0</a> <a id="4525" href="Cubical.Core.Primitives.html#4525" class="Bound">i</a> <a id="4527" class="Symbol">=</a>
  <a id="4531" href="Agda.Primitive.Cubical.html#1352" class="Primitive">hcomp</a> <a id="4537" class="Symbol">(λ</a> <a id="4540" href="Cubical.Core.Primitives.html#4540" class="Bound">j</a> <a id="4542" class="Symbol">→</a> <a id="4544" class="Symbol">λ</a> <a id="4546" class="Symbol">{</a> <a id="4548" class="Symbol">(</a><a id="4549" href="Cubical.Core.Primitives.html#4517" class="Bound">φ</a> <a id="4551" class="Symbol">=</a> <a id="4553" href="Agda.Primitive.Cubical.html#156" class="InductiveConstructor">i1</a><a id="4555" class="Symbol">)</a> <a id="4557" class="Symbol">→</a> <a id="4559" href="Cubical.Core.Primitives.html#4520" class="Bound">u</a> <a id="4561" class="Symbol">(</a><a id="4562" href="Cubical.Core.Primitives.html#4525" class="Bound">i</a> <a id="4564" href="Agda.Primitive.Cubical.html#226" class="Primitive Operator">∧</a> <a id="4566" href="Cubical.Core.Primitives.html#4540" class="Bound">j</a><a id="4567" class="Symbol">)</a> <a id="4569" href="Agda.Primitive.Cubical.html#362" class="Postulate">1=1</a>
                 <a id="4590" class="Symbol">;</a> <a id="4592" class="Symbol">(</a><a id="4593" href="Cubical.Core.Primitives.html#4525" class="Bound">i</a> <a id="4595" class="Symbol">=</a> <a id="4597" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a><a id="4599" class="Symbol">)</a> <a id="4601" class="Symbol">→</a> <a id="4603" href="Agda.Builtin.Cubical.Sub.html#290" class="Primitive">outS</a> <a id="4608" href="Cubical.Core.Primitives.html#4522" class="Bound">u0</a> <a id="4611" class="Symbol">})</a>
        <a id="4622" class="Symbol">(</a><a id="4623" href="Agda.Builtin.Cubical.Sub.html#290" class="Primitive">outS</a> <a id="4628" href="Cubical.Core.Primitives.html#4522" class="Bound">u0</a><a id="4630" class="Symbol">)</a>

<a id="4633" class="Comment">-- Heterogeneous composition defined as in CHM</a>
<a id="comp"></a><a id="4680" href="Cubical.Core.Primitives.html#4680" class="Function">comp</a> <a id="4685" class="Symbol">:</a> <a id="4687" class="Symbol">(</a><a id="4688" href="Cubical.Core.Primitives.html#4688" class="Bound">A</a> <a id="4690" class="Symbol">:</a> <a id="4692" class="Symbol">∀</a> <a id="4694" href="Cubical.Core.Primitives.html#4694" class="Bound">i</a> <a id="4696" class="Symbol">→</a> <a id="4698" class="PrimitiveType">Set</a> <a id="4702" class="Symbol">(</a><a id="4703" href="Cubical.Core.Primitives.html#4317" class="Generalizable">ℓ′</a> <a id="4706" href="Cubical.Core.Primitives.html#4694" class="Bound">i</a><a id="4707" class="Symbol">))</a>
       <a id="4717" class="Symbol">{</a><a id="4718" href="Cubical.Core.Primitives.html#4718" class="Bound">φ</a> <a id="4720" class="Symbol">:</a> <a id="4722" href="Agda.Primitive.Cubical.html#86" class="Datatype">I</a><a id="4723" class="Symbol">}</a>
       <a id="4732" class="Symbol">(</a><a id="4733" href="Cubical.Core.Primitives.html#4733" class="Bound">u</a> <a id="4735" class="Symbol">:</a> <a id="4737" class="Symbol">∀</a> <a id="4739" href="Cubical.Core.Primitives.html#4739" class="Bound">i</a> <a id="4741" class="Symbol">→</a> <a id="4743" href="Agda.Primitive.Cubical.html#684" class="Primitive">Partial</a> <a id="4751" href="Cubical.Core.Primitives.html#4718" class="Bound">φ</a> <a id="4753" class="Symbol">(</a><a id="4754" href="Cubical.Core.Primitives.html#4688" class="Bound">A</a> <a id="4756" href="Cubical.Core.Primitives.html#4739" class="Bound">i</a><a id="4757" class="Symbol">))</a>
       <a id="4767" class="Symbol">(</a><a id="4768" href="Cubical.Core.Primitives.html#4768" class="Bound">u0</a> <a id="4771" class="Symbol">:</a> <a id="4773" href="Cubical.Core.Primitives.html#4688" class="Bound">A</a> <a id="4775" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a> <a id="4778" href="Cubical.Core.Primitives.html#3131" class="Function Operator">[</a> <a id="4780" href="Cubical.Core.Primitives.html#4718" class="Bound">φ</a> <a id="4782" href="Cubical.Core.Primitives.html#3131" class="Function Operator">↦</a> <a id="4784" href="Cubical.Core.Primitives.html#4733" class="Bound">u</a> <a id="4786" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a> <a id="4789" href="Cubical.Core.Primitives.html#3131" class="Function Operator">]</a><a id="4790" class="Symbol">)</a>
     <a id="4797" class="Symbol">→</a> <a id="4799" class="Comment">---------------------------</a>
       <a id="4834" href="Cubical.Core.Primitives.html#4688" class="Bound">A</a> <a id="4836" href="Agda.Primitive.Cubical.html#156" class="InductiveConstructor">i1</a>
<a id="4839" href="Cubical.Core.Primitives.html#4680" class="Function">comp</a> <a id="4844" href="Cubical.Core.Primitives.html#4844" class="Bound">A</a> <a id="4846" class="Symbol">{</a><a id="4847" class="Argument">φ</a> <a id="4849" class="Symbol">=</a> <a id="4851" href="Cubical.Core.Primitives.html#4851" class="Bound">φ</a><a id="4852" class="Symbol">}</a> <a id="4854" href="Cubical.Core.Primitives.html#4854" class="Bound">u</a> <a id="4856" href="Cubical.Core.Primitives.html#4856" class="Bound">u0</a> <a id="4859" class="Symbol">=</a>
  <a id="4863" href="Agda.Primitive.Cubical.html#1352" class="Primitive">hcomp</a> <a id="4869" class="Symbol">(λ</a> <a id="4872" href="Cubical.Core.Primitives.html#4872" class="Bound">i</a> <a id="4874" class="Symbol">→</a> <a id="4876" class="Symbol">λ</a> <a id="4878" class="Symbol">{</a> <a id="4880" class="Symbol">(</a><a id="4881" href="Cubical.Core.Primitives.html#4851" class="Bound">φ</a> <a id="4883" class="Symbol">=</a> <a id="4885" href="Agda.Primitive.Cubical.html#156" class="InductiveConstructor">i1</a><a id="4887" class="Symbol">)</a> <a id="4889" class="Symbol">→</a> <a id="4891" href="Agda.Primitive.Cubical.html#1279" class="Primitive">transp</a> <a id="4898" class="Symbol">(λ</a> <a id="4901" href="Cubical.Core.Primitives.html#4901" class="Bound">j</a> <a id="4903" class="Symbol">→</a> <a id="4905" href="Cubical.Core.Primitives.html#4844" class="Bound">A</a> <a id="4907" class="Symbol">(</a><a id="4908" href="Cubical.Core.Primitives.html#4872" class="Bound">i</a> <a id="4910" href="Agda.Primitive.Cubical.html#251" class="Primitive Operator">∨</a> <a id="4912" href="Cubical.Core.Primitives.html#4901" class="Bound">j</a><a id="4913" class="Symbol">))</a> <a id="4916" href="Cubical.Core.Primitives.html#4872" class="Bound">i</a> <a id="4918" class="Symbol">(</a><a id="4919" href="Cubical.Core.Primitives.html#4854" class="Bound">u</a> <a id="4921" class="Symbol">_</a> <a id="4923" href="Agda.Primitive.Cubical.html#362" class="Postulate">1=1</a><a id="4926" class="Symbol">)</a> <a id="4928" class="Symbol">})</a>
        <a id="4939" class="Symbol">(</a><a id="4940" href="Agda.Primitive.Cubical.html#1279" class="Primitive">transp</a> <a id="4947" href="Cubical.Core.Primitives.html#4844" class="Bound">A</a> <a id="4949" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a> <a id="4952" class="Symbol">(</a><a id="4953" href="Agda.Builtin.Cubical.Sub.html#290" class="Primitive">outS</a> <a id="4958" href="Cubical.Core.Primitives.html#4856" class="Bound">u0</a><a id="4960" class="Symbol">))</a>

<a id="4964" class="Comment">-- Heterogeneous filling defined using comp</a>
<a id="fill"></a><a id="5008" href="Cubical.Core.Primitives.html#5008" class="Function">fill</a> <a id="5013" class="Symbol">:</a> <a id="5015" class="Symbol">(</a><a id="5016" href="Cubical.Core.Primitives.html#5016" class="Bound">A</a> <a id="5018" class="Symbol">:</a> <a id="5020" class="Symbol">∀</a> <a id="5022" href="Cubical.Core.Primitives.html#5022" class="Bound">i</a> <a id="5024" class="Symbol">→</a> <a id="5026" class="PrimitiveType">Set</a> <a id="5030" class="Symbol">(</a><a id="5031" href="Cubical.Core.Primitives.html#4317" class="Generalizable">ℓ′</a> <a id="5034" href="Cubical.Core.Primitives.html#5022" class="Bound">i</a><a id="5035" class="Symbol">))</a>
       <a id="5045" class="Symbol">{</a><a id="5046" href="Cubical.Core.Primitives.html#5046" class="Bound">φ</a> <a id="5048" class="Symbol">:</a> <a id="5050" href="Agda.Primitive.Cubical.html#86" class="Datatype">I</a><a id="5051" class="Symbol">}</a>
       <a id="5060" class="Symbol">(</a><a id="5061" href="Cubical.Core.Primitives.html#5061" class="Bound">u</a> <a id="5063" class="Symbol">:</a> <a id="5065" class="Symbol">∀</a> <a id="5067" href="Cubical.Core.Primitives.html#5067" class="Bound">i</a> <a id="5069" class="Symbol">→</a> <a id="5071" href="Agda.Primitive.Cubical.html#684" class="Primitive">Partial</a> <a id="5079" href="Cubical.Core.Primitives.html#5046" class="Bound">φ</a> <a id="5081" class="Symbol">(</a><a id="5082" href="Cubical.Core.Primitives.html#5016" class="Bound">A</a> <a id="5084" href="Cubical.Core.Primitives.html#5067" class="Bound">i</a><a id="5085" class="Symbol">))</a>
       <a id="5095" class="Symbol">(</a><a id="5096" href="Cubical.Core.Primitives.html#5096" class="Bound">u0</a> <a id="5099" class="Symbol">:</a> <a id="5101" href="Cubical.Core.Primitives.html#5016" class="Bound">A</a> <a id="5103" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a> <a id="5106" href="Cubical.Core.Primitives.html#3131" class="Function Operator">[</a> <a id="5108" href="Cubical.Core.Primitives.html#5046" class="Bound">φ</a> <a id="5110" href="Cubical.Core.Primitives.html#3131" class="Function Operator">↦</a> <a id="5112" href="Cubical.Core.Primitives.html#5061" class="Bound">u</a> <a id="5114" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a> <a id="5117" href="Cubical.Core.Primitives.html#3131" class="Function Operator">]</a><a id="5118" class="Symbol">)</a>
       <a id="5127" class="Comment">---------------------------</a>
       <a id="5162" class="Symbol">(</a><a id="5163" href="Cubical.Core.Primitives.html#5163" class="Bound">i</a> <a id="5165" class="Symbol">:</a> <a id="5167" href="Agda.Primitive.Cubical.html#86" class="Datatype">I</a><a id="5168" class="Symbol">)</a> <a id="5170" class="Symbol">→</a> <a id="5172" href="Cubical.Core.Primitives.html#5016" class="Bound">A</a> <a id="5174" href="Cubical.Core.Primitives.html#5163" class="Bound">i</a>
<a id="5176" href="Cubical.Core.Primitives.html#5008" class="Function">fill</a> <a id="5181" href="Cubical.Core.Primitives.html#5181" class="Bound">A</a> <a id="5183" class="Symbol">{</a><a id="5184" class="Argument">φ</a> <a id="5186" class="Symbol">=</a> <a id="5188" href="Cubical.Core.Primitives.html#5188" class="Bound">φ</a><a id="5189" class="Symbol">}</a> <a id="5191" href="Cubical.Core.Primitives.html#5191" class="Bound">u</a> <a id="5193" href="Cubical.Core.Primitives.html#5193" class="Bound">u0</a> <a id="5196" href="Cubical.Core.Primitives.html#5196" class="Bound">i</a> <a id="5198" class="Symbol">=</a>
  <a id="5202" href="Cubical.Core.Primitives.html#4680" class="Function">comp</a> <a id="5207" class="Symbol">(λ</a> <a id="5210" href="Cubical.Core.Primitives.html#5210" class="Bound">j</a> <a id="5212" class="Symbol">→</a> <a id="5214" href="Cubical.Core.Primitives.html#5181" class="Bound">A</a> <a id="5216" class="Symbol">(</a><a id="5217" href="Cubical.Core.Primitives.html#5196" class="Bound">i</a> <a id="5219" href="Agda.Primitive.Cubical.html#226" class="Primitive Operator">∧</a> <a id="5221" href="Cubical.Core.Primitives.html#5210" class="Bound">j</a><a id="5222" class="Symbol">))</a>
       <a id="5232" class="Symbol">(λ</a> <a id="5235" href="Cubical.Core.Primitives.html#5235" class="Bound">j</a> <a id="5237" class="Symbol">→</a> <a id="5239" class="Symbol">λ</a> <a id="5241" class="Symbol">{</a> <a id="5243" class="Symbol">(</a><a id="5244" href="Cubical.Core.Primitives.html#5188" class="Bound">φ</a> <a id="5246" class="Symbol">=</a> <a id="5248" href="Agda.Primitive.Cubical.html#156" class="InductiveConstructor">i1</a><a id="5250" class="Symbol">)</a> <a id="5252" class="Symbol">→</a> <a id="5254" href="Cubical.Core.Primitives.html#5191" class="Bound">u</a> <a id="5256" class="Symbol">(</a><a id="5257" href="Cubical.Core.Primitives.html#5196" class="Bound">i</a> <a id="5259" href="Agda.Primitive.Cubical.html#226" class="Primitive Operator">∧</a> <a id="5261" href="Cubical.Core.Primitives.html#5235" class="Bound">j</a><a id="5262" class="Symbol">)</a> <a id="5264" href="Agda.Primitive.Cubical.html#362" class="Postulate">1=1</a>
                <a id="5284" class="Symbol">;</a> <a id="5286" class="Symbol">(</a><a id="5287" href="Cubical.Core.Primitives.html#5196" class="Bound">i</a> <a id="5289" class="Symbol">=</a> <a id="5291" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a><a id="5293" class="Symbol">)</a> <a id="5295" class="Symbol">→</a> <a id="5297" href="Agda.Builtin.Cubical.Sub.html#290" class="Primitive">outS</a> <a id="5302" href="Cubical.Core.Primitives.html#5193" class="Bound">u0</a> <a id="5305" class="Symbol">})</a>
       <a id="5315" class="Symbol">(</a><a id="5316" href="Agda.Builtin.Cubical.Sub.html#188" class="Postulate">inS</a> <a id="5320" class="Symbol">{</a><a id="5321" class="Argument">φ</a> <a id="5323" class="Symbol">=</a> <a id="5325" href="Cubical.Core.Primitives.html#5188" class="Bound">φ</a> <a id="5327" href="Agda.Primitive.Cubical.html#251" class="Primitive Operator">∨</a> <a id="5329" class="Symbol">(</a><a id="5330" href="Agda.Primitive.Cubical.html#276" class="Primitive Operator">~</a> <a id="5332" href="Cubical.Core.Primitives.html#5196" class="Bound">i</a><a id="5333" class="Symbol">)}</a> <a id="5336" class="Symbol">(</a><a id="5337" href="Agda.Builtin.Cubical.Sub.html#290" class="Primitive">outS</a> <a id="5342" class="Symbol">{</a><a id="5343" class="Argument">φ</a> <a id="5345" class="Symbol">=</a> <a id="5347" href="Cubical.Core.Primitives.html#5188" class="Bound">φ</a><a id="5348" class="Symbol">}</a> <a id="5350" href="Cubical.Core.Primitives.html#5193" class="Bound">u0</a><a id="5352" class="Symbol">))</a>


<a id="5357" class="Comment">-- Direct definition of transport filler, note that we have to</a>
<a id="5420" class="Comment">-- explicitly tell Agda that the type is constant (like in CHM)</a>
<a id="transpFill"></a><a id="5484" href="Cubical.Core.Primitives.html#5484" class="Function">transpFill</a> <a id="5495" class="Symbol">:</a> <a id="5497" class="Symbol">{</a><a id="5498" href="Cubical.Core.Primitives.html#5498" class="Bound">A</a> <a id="5500" class="Symbol">:</a> <a id="5502" class="PrimitiveType">Set</a> <a id="5506" href="Cubical.Core.Primitives.html#4302" class="Generalizable">ℓ</a><a id="5507" class="Symbol">}</a>
             <a id="5522" class="Symbol">(</a><a id="5523" href="Cubical.Core.Primitives.html#5523" class="Bound">φ</a> <a id="5525" class="Symbol">:</a> <a id="5527" href="Agda.Primitive.Cubical.html#86" class="Datatype">I</a><a id="5528" class="Symbol">)</a>
             <a id="5543" class="Symbol">(</a><a id="5544" href="Cubical.Core.Primitives.html#5544" class="Bound">A</a> <a id="5546" class="Symbol">:</a> <a id="5548" class="Symbol">(</a><a id="5549" href="Cubical.Core.Primitives.html#5549" class="Bound">i</a> <a id="5551" class="Symbol">:</a> <a id="5553" href="Agda.Primitive.Cubical.html#86" class="Datatype">I</a><a id="5554" class="Symbol">)</a> <a id="5556" class="Symbol">→</a> <a id="5558" class="PrimitiveType">Set</a> <a id="5562" href="Cubical.Core.Primitives.html#4302" class="Generalizable">ℓ</a> <a id="5564" href="Cubical.Core.Primitives.html#3131" class="Function Operator">[</a> <a id="5566" href="Cubical.Core.Primitives.html#5523" class="Bound">φ</a> <a id="5568" href="Cubical.Core.Primitives.html#3131" class="Function Operator">↦</a> <a id="5570" class="Symbol">(λ</a> <a id="5573" href="Cubical.Core.Primitives.html#5573" class="Bound">_</a> <a id="5575" class="Symbol">→</a> <a id="5577" href="Cubical.Core.Primitives.html#5498" class="Bound">A</a><a id="5578" class="Symbol">)</a> <a id="5580" href="Cubical.Core.Primitives.html#3131" class="Function Operator">]</a><a id="5581" class="Symbol">)</a>
             <a id="5596" class="Symbol">(</a><a id="5597" href="Cubical.Core.Primitives.html#5597" class="Bound">u0</a> <a id="5600" class="Symbol">:</a> <a id="5602" href="Agda.Builtin.Cubical.Sub.html#290" class="Primitive">outS</a> <a id="5607" class="Symbol">(</a><a id="5608" href="Cubical.Core.Primitives.html#5544" class="Bound">A</a> <a id="5610" href="Agda.Primitive.Cubical.html#128" class="InductiveConstructor">i0</a><a id="5612" class="Symbol">))</a>
           <a id="5626" class="Symbol">→</a> <a id="5628" class="Comment">--------------------------------------</a>
             <a id="5680" href="Agda.Builtin.Cubical.Path.html#162" class="Postulate">PathP</a> <a id="5686" class="Symbol">(λ</a> <a id="5689" href="Cubical.Core.Primitives.html#5689" class="Bound">i</a> <a id="5691" class="Symbol">→</a> <a id="5693" href="Agda.Builtin.Cubical.Sub.html#290" class="Primitive">outS</a> <a id="5698" class="Symbol">(</a><a id="5699" href="Cubical.Core.Primitives.html#5544" class="Bound">A</a> <a id="5701" href="Cubical.Core.Primitives.html#5689" class="Bound">i</a><a id="5702" class="Symbol">))</a> <a id="5705" href="Cubical.Core.Primitives.html#5597" class="Bound">u0</a> <a id="5708" class="Symbol">(</a><a id="5709" href="Agda.Primitive.Cubical.html#1279" class="Primitive">transp</a> <a id="5716" class="Symbol">(λ</a> <a id="5719" href="Cubical.Core.Primitives.html#5719" class="Bound">i</a> <a id="5721" class="Symbol">→</a> <a id="5723" href="Agda.Builtin.Cubical.Sub.html#290" class="Primitive">outS</a> <a id="5728" class="Symbol">(</a><a id="5729" href="Cubical.Core.Primitives.html#5544" class="Bound">A</a> <a id="5731" href="Cubical.Core.Primitives.html#5719" class="Bound">i</a><a id="5732" class="Symbol">))</a> <a id="5735" href="Cubical.Core.Primitives.html#5523" class="Bound">φ</a> <a id="5737" href="Cubical.Core.Primitives.html#5597" class="Bound">u0</a><a id="5739" class="Symbol">)</a>
<a id="5741" href="Cubical.Core.Primitives.html#5484" class="Function">transpFill</a> <a id="5752" href="Cubical.Core.Primitives.html#5752" class="Bound">φ</a> <a id="5754" href="Cubical.Core.Primitives.html#5754" class="Bound">A</a> <a id="5756" href="Cubical.Core.Primitives.html#5756" class="Bound">u0</a> <a id="5759" href="Cubical.Core.Primitives.html#5759" class="Bound">i</a> <a id="5761" class="Symbol">=</a> <a id="5763" href="Agda.Primitive.Cubical.html#1279" class="Primitive">transp</a> <a id="5770" class="Symbol">(λ</a> <a id="5773" href="Cubical.Core.Primitives.html#5773" class="Bound">j</a> <a id="5775" class="Symbol">→</a> <a id="5777" href="Agda.Builtin.Cubical.Sub.html#290" class="Primitive">outS</a> <a id="5782" class="Symbol">(</a><a id="5783" href="Cubical.Core.Primitives.html#5754" class="Bound">A</a> <a id="5785" class="Symbol">(</a><a id="5786" href="Cubical.Core.Primitives.html#5759" class="Bound">i</a> <a id="5788" href="Agda.Primitive.Cubical.html#226" class="Primitive Operator">∧</a> <a id="5790" href="Cubical.Core.Primitives.html#5773" class="Bound">j</a><a id="5791" class="Symbol">)))</a> <a id="5795" class="Symbol">(</a><a id="5796" href="Agda.Primitive.Cubical.html#276" class="Primitive Operator">~</a> <a id="5798" href="Cubical.Core.Primitives.html#5759" class="Bound">i</a> <a id="5800" href="Agda.Primitive.Cubical.html#251" class="Primitive Operator">∨</a> <a id="5802" href="Cubical.Core.Primitives.html#5752" class="Bound">φ</a><a id="5803" class="Symbol">)</a> <a id="5805" href="Cubical.Core.Primitives.html#5756" class="Bound">u0</a>


<a id="5810" class="Comment">-- Σ-types</a>
<a id="5821" class="Keyword">infix</a> <a id="5827" class="Number">2</a> <a id="5829" href="Cubical.Core.Primitives.html#5839" class="Function">Σ-syntax</a>

<a id="Σ-syntax"></a><a id="5839" href="Cubical.Core.Primitives.html#5839" class="Function">Σ-syntax</a> <a id="5848" class="Symbol">:</a> <a id="5850" class="Symbol">∀</a> <a id="5852" class="Symbol">{</a><a id="5853" href="Cubical.Core.Primitives.html#5853" class="Bound">ℓ</a> <a id="5855" href="Cubical.Core.Primitives.html#5855" class="Bound">ℓ'</a><a id="5857" class="Symbol">}</a> <a id="5859" class="Symbol">(</a><a id="5860" href="Cubical.Core.Primitives.html#5860" class="Bound">A</a> <a id="5862" class="Symbol">:</a> <a id="5864" class="PrimitiveType">Set</a> <a id="5868" href="Cubical.Core.Primitives.html#5853" class="Bound">ℓ</a><a id="5869" class="Symbol">)</a> <a id="5871" class="Symbol">(</a><a id="5872" href="Cubical.Core.Primitives.html#5872" class="Bound">B</a> <a id="5874" class="Symbol">:</a> <a id="5876" href="Cubical.Core.Primitives.html#5860" class="Bound">A</a> <a id="5878" class="Symbol">→</a> <a id="5880" class="PrimitiveType">Set</a> <a id="5884" href="Cubical.Core.Primitives.html#5855" class="Bound">ℓ'</a><a id="5886" class="Symbol">)</a> <a id="5888" class="Symbol">→</a> <a id="5890" class="PrimitiveType">Set</a> <a id="5894" class="Symbol">(</a><a id="5895" href="Agda.Primitive.html#657" class="Primitive">ℓ-max</a> <a id="5901" href="Cubical.Core.Primitives.html#5853" class="Bound">ℓ</a> <a id="5903" href="Cubical.Core.Primitives.html#5855" class="Bound">ℓ'</a><a id="5905" class="Symbol">)</a>
<a id="5907" href="Cubical.Core.Primitives.html#5839" class="Function">Σ-syntax</a> <a id="5916" class="Symbol">=</a> <a id="5918" href="Agda.Builtin.Sigma.html#139" class="Record">Σ</a>

<a id="5921" class="Keyword">syntax</a> <a id="5928" href="Cubical.Core.Primitives.html#5839" class="Function">Σ-syntax</a> <a id="5937" class="Bound">A</a> <a id="5939" class="Symbol">(λ</a> <a id="5942" class="Bound">x</a> <a id="5944" class="Symbol">→</a> <a id="5946" class="Bound">B</a><a id="5947" class="Symbol">)</a> <a id="5949" class="Symbol">=</a> <a id="5951" class="Function">Σ[</a> <a id="5954" class="Bound">x</a> <a id="5956" class="Function">∈</a> <a id="5958" class="Bound">A</a> <a id="5960" class="Function">]</a> <a id="5962" class="Bound">B</a>

</pre></body></html>