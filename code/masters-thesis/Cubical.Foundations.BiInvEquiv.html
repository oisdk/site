<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Cubical.Foundations.BiInvEquiv</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">{-

Some theory about Bi-Invertible Equivalences

- BiInvEquiv to Iso
- BiInvEquiv to Equiv
- BiInvEquiv to HAEquiv
- Iso to BiInvEquiv

-}</a>
<a id="141" class="Symbol">{-#</a> <a id="145" class="Keyword">OPTIONS</a> <a id="153" class="Pragma">--cubical</a> <a id="163" class="Pragma">--safe</a> <a id="170" class="Symbol">#-}</a>
<a id="174" class="Keyword">module</a> <a id="181" href="Cubical.Foundations.BiInvEquiv.html" class="Module">Cubical.Foundations.BiInvEquiv</a> <a id="212" class="Keyword">where</a>

<a id="219" class="Keyword">open</a> <a id="224" class="Keyword">import</a> <a id="231" href="Cubical.Foundations.Prelude.html" class="Module">Cubical.Foundations.Prelude</a>
<a id="259" class="Keyword">open</a> <a id="264" class="Keyword">import</a> <a id="271" href="Cubical.Foundations.Function.html" class="Module">Cubical.Foundations.Function</a>
<a id="300" class="Keyword">open</a> <a id="305" class="Keyword">import</a> <a id="312" href="Cubical.Foundations.Isomorphism.html" class="Module">Cubical.Foundations.Isomorphism</a>
<a id="344" class="Keyword">open</a> <a id="349" class="Keyword">import</a> <a id="356" href="Cubical.Foundations.Equiv.html" class="Module">Cubical.Foundations.Equiv</a>
<a id="382" class="Keyword">open</a> <a id="387" class="Keyword">import</a> <a id="394" href="Cubical.Foundations.HAEquiv.html" class="Module">Cubical.Foundations.HAEquiv</a>


<a id="424" class="Keyword">record</a> <a id="BiInvEquiv"></a><a id="431" href="Cubical.Foundations.BiInvEquiv.html#431" class="Record">BiInvEquiv</a> <a id="442" class="Symbol">{</a><a id="443" href="Cubical.Foundations.BiInvEquiv.html#443" class="Bound">ℓ</a> <a id="445" href="Cubical.Foundations.BiInvEquiv.html#445" class="Bound">ℓ&#39;</a><a id="447" class="Symbol">}</a> <a id="449" class="Symbol">(</a><a id="450" href="Cubical.Foundations.BiInvEquiv.html#450" class="Bound">A</a> <a id="452" class="Symbol">:</a> <a id="454" href="Cubical.Core.Primitives.html#957" class="Function">Type</a> <a id="459" href="Cubical.Foundations.BiInvEquiv.html#443" class="Bound">ℓ</a><a id="460" class="Symbol">)</a> <a id="462" class="Symbol">(</a><a id="463" href="Cubical.Foundations.BiInvEquiv.html#463" class="Bound">B</a> <a id="465" class="Symbol">:</a> <a id="467" href="Cubical.Core.Primitives.html#957" class="Function">Type</a> <a id="472" href="Cubical.Foundations.BiInvEquiv.html#445" class="Bound">ℓ&#39;</a><a id="474" class="Symbol">)</a> <a id="476" class="Symbol">:</a> <a id="478" href="Cubical.Core.Primitives.html#957" class="Function">Type</a> <a id="483" class="Symbol">(</a><a id="484" href="Agda.Primitive.html#636" class="Primitive">ℓ-max</a> <a id="490" href="Cubical.Foundations.BiInvEquiv.html#443" class="Bound">ℓ</a> <a id="492" href="Cubical.Foundations.BiInvEquiv.html#445" class="Bound">ℓ&#39;</a><a id="494" class="Symbol">)</a> <a id="496" class="Keyword">where</a>
  <a id="504" class="Keyword">constructor</a> <a id="biInvEquiv"></a><a id="516" href="Cubical.Foundations.BiInvEquiv.html#516" class="InductiveConstructor">biInvEquiv</a>
  <a id="529" class="Keyword">field</a>
    <a id="BiInvEquiv.fun"></a><a id="539" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="543" class="Symbol">:</a> <a id="545" href="Cubical.Foundations.BiInvEquiv.html#450" class="Bound">A</a> <a id="547" class="Symbol">→</a> <a id="549" href="Cubical.Foundations.BiInvEquiv.html#463" class="Bound">B</a>
    <a id="BiInvEquiv.invr"></a><a id="555" href="Cubical.Foundations.BiInvEquiv.html#555" class="Field">invr</a> <a id="560" class="Symbol">:</a> <a id="562" href="Cubical.Foundations.BiInvEquiv.html#463" class="Bound">B</a> <a id="564" class="Symbol">→</a> <a id="566" href="Cubical.Foundations.BiInvEquiv.html#450" class="Bound">A</a>
    <a id="BiInvEquiv.invr-rightInv"></a><a id="572" href="Cubical.Foundations.BiInvEquiv.html#572" class="Field">invr-rightInv</a> <a id="586" class="Symbol">:</a> <a id="588" href="Cubical.Foundations.Isomorphism.html#430" class="Function">section</a> <a id="596" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="600" href="Cubical.Foundations.BiInvEquiv.html#555" class="Field">invr</a>
    <a id="BiInvEquiv.invl"></a><a id="609" href="Cubical.Foundations.BiInvEquiv.html#609" class="Field">invl</a> <a id="614" class="Symbol">:</a> <a id="616" href="Cubical.Foundations.BiInvEquiv.html#463" class="Bound">B</a> <a id="618" class="Symbol">→</a> <a id="620" href="Cubical.Foundations.BiInvEquiv.html#450" class="Bound">A</a>
    <a id="BiInvEquiv.invl-leftInv"></a><a id="626" href="Cubical.Foundations.BiInvEquiv.html#626" class="Field">invl-leftInv</a> <a id="639" class="Symbol">:</a> <a id="641" href="Cubical.Foundations.Isomorphism.html#545" class="Function">retract</a> <a id="649" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="653" href="Cubical.Foundations.BiInvEquiv.html#609" class="Field">invl</a>

  <a id="BiInvEquiv.invr≡invl"></a><a id="661" href="Cubical.Foundations.BiInvEquiv.html#661" class="Function">invr≡invl</a> <a id="671" class="Symbol">:</a> <a id="673" class="Symbol">∀</a> <a id="675" href="Cubical.Foundations.BiInvEquiv.html#675" class="Bound">b</a> <a id="677" class="Symbol">→</a> <a id="679" href="Cubical.Foundations.BiInvEquiv.html#555" class="Field">invr</a> <a id="684" href="Cubical.Foundations.BiInvEquiv.html#675" class="Bound">b</a> <a id="686" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="688" href="Cubical.Foundations.BiInvEquiv.html#609" class="Field">invl</a> <a id="693" href="Cubical.Foundations.BiInvEquiv.html#675" class="Bound">b</a>
  <a id="697" href="Cubical.Foundations.BiInvEquiv.html#661" class="Function">invr≡invl</a> <a id="707" href="Cubical.Foundations.BiInvEquiv.html#707" class="Bound">b</a> <a id="709" class="Symbol">=</a>            <a id="722" href="Cubical.Foundations.BiInvEquiv.html#555" class="Field">invr</a> <a id="727" href="Cubical.Foundations.BiInvEquiv.html#707" class="Bound">b</a>   <a id="731" href="Cubical.Foundations.Prelude.html#2574" class="Function Operator">≡⟨</a> <a id="734" href="Cubical.Foundations.Prelude.html#893" class="Function">sym</a> <a id="738" class="Symbol">(</a><a id="739" href="Cubical.Foundations.BiInvEquiv.html#626" class="Field">invl-leftInv</a> <a id="752" class="Symbol">(</a><a id="753" href="Cubical.Foundations.BiInvEquiv.html#555" class="Field">invr</a> <a id="758" href="Cubical.Foundations.BiInvEquiv.html#707" class="Bound">b</a><a id="759" class="Symbol">))</a> <a id="762" href="Cubical.Foundations.Prelude.html#2574" class="Function Operator">⟩</a>
                <a id="780" href="Cubical.Foundations.BiInvEquiv.html#609" class="Field">invl</a> <a id="785" class="Symbol">(</a><a id="786" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="790" class="Symbol">(</a><a id="791" href="Cubical.Foundations.BiInvEquiv.html#555" class="Field">invr</a> <a id="796" href="Cubical.Foundations.BiInvEquiv.html#707" class="Bound">b</a><a id="797" class="Symbol">))</a> <a id="800" href="Cubical.Foundations.Prelude.html#2574" class="Function Operator">≡⟨</a> <a id="803" href="Cubical.Foundations.Prelude.html#1057" class="Function">cong</a> <a id="808" href="Cubical.Foundations.BiInvEquiv.html#609" class="Field">invl</a> <a id="813" class="Symbol">(</a><a id="814" href="Cubical.Foundations.BiInvEquiv.html#572" class="Field">invr-rightInv</a> <a id="828" href="Cubical.Foundations.BiInvEquiv.html#707" class="Bound">b</a><a id="829" class="Symbol">)</a> <a id="831" href="Cubical.Foundations.Prelude.html#2574" class="Function Operator">⟩</a>
                <a id="849" href="Cubical.Foundations.BiInvEquiv.html#609" class="Field">invl</a> <a id="854" href="Cubical.Foundations.BiInvEquiv.html#707" class="Bound">b</a>              <a id="869" href="Cubical.Foundations.Prelude.html#2780" class="Function Operator">∎</a>

  <a id="BiInvEquiv.invr-leftInv"></a><a id="874" href="Cubical.Foundations.BiInvEquiv.html#874" class="Function">invr-leftInv</a> <a id="887" class="Symbol">:</a> <a id="889" href="Cubical.Foundations.Isomorphism.html#545" class="Function">retract</a> <a id="897" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="901" href="Cubical.Foundations.BiInvEquiv.html#555" class="Field">invr</a>
  <a id="908" href="Cubical.Foundations.BiInvEquiv.html#874" class="Function">invr-leftInv</a> <a id="921" href="Cubical.Foundations.BiInvEquiv.html#921" class="Bound">a</a> <a id="923" class="Symbol">=</a> <a id="925" href="Cubical.Foundations.BiInvEquiv.html#661" class="Function">invr≡invl</a> <a id="935" class="Symbol">(</a><a id="936" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="940" href="Cubical.Foundations.BiInvEquiv.html#921" class="Bound">a</a><a id="941" class="Symbol">)</a> <a id="943" href="Cubical.Foundations.Prelude.html#3058" class="Function Operator">□</a> <a id="945" class="Symbol">(</a><a id="946" href="Cubical.Foundations.BiInvEquiv.html#626" class="Field">invl-leftInv</a> <a id="959" href="Cubical.Foundations.BiInvEquiv.html#921" class="Bound">a</a><a id="960" class="Symbol">)</a>

  <a id="BiInvEquiv.invr≡invl-leftInv"></a><a id="965" href="Cubical.Foundations.BiInvEquiv.html#965" class="Function">invr≡invl-leftInv</a> <a id="983" class="Symbol">:</a> <a id="985" class="Symbol">∀</a> <a id="987" href="Cubical.Foundations.BiInvEquiv.html#987" class="Bound">a</a> <a id="989" class="Symbol">→</a> <a id="991" href="Agda.Builtin.Cubical.Path.html#190" class="Postulate">PathP</a> <a id="997" class="Symbol">(λ</a> <a id="1000" href="Cubical.Foundations.BiInvEquiv.html#1000" class="Bound">j</a> <a id="1002" class="Symbol">→</a> <a id="1004" href="Cubical.Foundations.BiInvEquiv.html#661" class="Function">invr≡invl</a> <a id="1014" class="Symbol">(</a><a id="1015" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="1019" href="Cubical.Foundations.BiInvEquiv.html#987" class="Bound">a</a><a id="1020" class="Symbol">)</a> <a id="1022" href="Cubical.Foundations.BiInvEquiv.html#1000" class="Bound">j</a> <a id="1024" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="1026" href="Cubical.Foundations.BiInvEquiv.html#987" class="Bound">a</a><a id="1027" class="Symbol">)</a> <a id="1029" class="Symbol">(</a><a id="1030" href="Cubical.Foundations.BiInvEquiv.html#874" class="Function">invr-leftInv</a> <a id="1043" href="Cubical.Foundations.BiInvEquiv.html#987" class="Bound">a</a><a id="1044" class="Symbol">)</a> <a id="1046" class="Symbol">(</a><a id="1047" href="Cubical.Foundations.BiInvEquiv.html#626" class="Field">invl-leftInv</a> <a id="1060" href="Cubical.Foundations.BiInvEquiv.html#987" class="Bound">a</a><a id="1061" class="Symbol">)</a>
  <a id="1065" href="Cubical.Foundations.BiInvEquiv.html#965" class="Function">invr≡invl-leftInv</a> <a id="1083" href="Cubical.Foundations.BiInvEquiv.html#1083" class="Bound">a</a> <a id="1085" href="Cubical.Foundations.BiInvEquiv.html#1085" class="Bound">j</a> <a id="1087" href="Cubical.Foundations.BiInvEquiv.html#1087" class="Bound">i</a> <a id="1089" class="Symbol">=</a> <a id="1091" href="Cubical.Foundations.Prelude.html#2874" class="Function">compPath&#39;-filler</a> <a id="1108" class="Symbol">(</a><a id="1109" href="Cubical.Foundations.BiInvEquiv.html#661" class="Function">invr≡invl</a> <a id="1119" class="Symbol">(</a><a id="1120" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="1124" href="Cubical.Foundations.BiInvEquiv.html#1083" class="Bound">a</a><a id="1125" class="Symbol">))</a> <a id="1128" class="Symbol">(</a><a id="1129" href="Cubical.Foundations.BiInvEquiv.html#626" class="Field">invl-leftInv</a> <a id="1142" href="Cubical.Foundations.BiInvEquiv.html#1083" class="Bound">a</a><a id="1143" class="Symbol">)</a> <a id="1145" class="Symbol">(</a><a id="1146" href="Cubical.Core.Primitives.html#549" class="Primitive Operator">~</a> <a id="1148" href="Cubical.Foundations.BiInvEquiv.html#1085" class="Bound">j</a><a id="1149" class="Symbol">)</a> <a id="1151" href="Cubical.Foundations.BiInvEquiv.html#1087" class="Bound">i</a>

  <a id="BiInvEquiv.invl-rightInv"></a><a id="1156" href="Cubical.Foundations.BiInvEquiv.html#1156" class="Function">invl-rightInv</a> <a id="1170" class="Symbol">:</a> <a id="1172" href="Cubical.Foundations.Isomorphism.html#430" class="Function">section</a> <a id="1180" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="1184" href="Cubical.Foundations.BiInvEquiv.html#609" class="Field">invl</a>
  <a id="1191" href="Cubical.Foundations.BiInvEquiv.html#1156" class="Function">invl-rightInv</a> <a id="1205" href="Cubical.Foundations.BiInvEquiv.html#1205" class="Bound">a</a> <a id="1207" class="Symbol">=</a> <a id="1209" href="Cubical.Foundations.Prelude.html#893" class="Function">sym</a> <a id="1213" class="Symbol">(</a><a id="1214" href="Cubical.Foundations.Prelude.html#1057" class="Function">cong</a> <a id="1219" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="1223" class="Symbol">(</a><a id="1224" href="Cubical.Foundations.BiInvEquiv.html#661" class="Function">invr≡invl</a> <a id="1234" href="Cubical.Foundations.BiInvEquiv.html#1205" class="Bound">a</a><a id="1235" class="Symbol">))</a> <a id="1238" href="Cubical.Foundations.Prelude.html#3058" class="Function Operator">□</a> <a id="1240" class="Symbol">(</a><a id="1241" href="Cubical.Foundations.BiInvEquiv.html#572" class="Field">invr-rightInv</a> <a id="1255" href="Cubical.Foundations.BiInvEquiv.html#1205" class="Bound">a</a><a id="1256" class="Symbol">)</a>

  <a id="BiInvEquiv.invr≡invl-rightInv"></a><a id="1261" href="Cubical.Foundations.BiInvEquiv.html#1261" class="Function">invr≡invl-rightInv</a> <a id="1280" class="Symbol">:</a> <a id="1282" class="Symbol">∀</a> <a id="1284" href="Cubical.Foundations.BiInvEquiv.html#1284" class="Bound">a</a> <a id="1286" class="Symbol">→</a> <a id="1288" href="Agda.Builtin.Cubical.Path.html#190" class="Postulate">PathP</a> <a id="1294" class="Symbol">(λ</a> <a id="1297" href="Cubical.Foundations.BiInvEquiv.html#1297" class="Bound">j</a> <a id="1299" class="Symbol">→</a> <a id="1301" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="1305" class="Symbol">(</a><a id="1306" href="Cubical.Foundations.BiInvEquiv.html#661" class="Function">invr≡invl</a> <a id="1316" href="Cubical.Foundations.BiInvEquiv.html#1284" class="Bound">a</a> <a id="1318" href="Cubical.Foundations.BiInvEquiv.html#1297" class="Bound">j</a><a id="1319" class="Symbol">)</a> <a id="1321" href="Agda.Builtin.Cubical.Path.html#381" class="Function Operator">≡</a> <a id="1323" href="Cubical.Foundations.BiInvEquiv.html#1284" class="Bound">a</a><a id="1324" class="Symbol">)</a> <a id="1326" class="Symbol">(</a><a id="1327" href="Cubical.Foundations.BiInvEquiv.html#572" class="Field">invr-rightInv</a> <a id="1341" href="Cubical.Foundations.BiInvEquiv.html#1284" class="Bound">a</a><a id="1342" class="Symbol">)</a> <a id="1344" class="Symbol">(</a><a id="1345" href="Cubical.Foundations.BiInvEquiv.html#1156" class="Function">invl-rightInv</a> <a id="1359" href="Cubical.Foundations.BiInvEquiv.html#1284" class="Bound">a</a><a id="1360" class="Symbol">)</a>
  <a id="1364" href="Cubical.Foundations.BiInvEquiv.html#1261" class="Function">invr≡invl-rightInv</a> <a id="1383" href="Cubical.Foundations.BiInvEquiv.html#1383" class="Bound">a</a> <a id="1385" href="Cubical.Foundations.BiInvEquiv.html#1385" class="Bound">j</a> <a id="1387" href="Cubical.Foundations.BiInvEquiv.html#1387" class="Bound">i</a> <a id="1389" class="Symbol">=</a> <a id="1391" href="Cubical.Foundations.Prelude.html#2874" class="Function">compPath&#39;-filler</a> <a id="1408" class="Symbol">(</a><a id="1409" href="Cubical.Foundations.Prelude.html#893" class="Function">sym</a> <a id="1413" class="Symbol">(</a><a id="1414" href="Cubical.Foundations.Prelude.html#1057" class="Function">cong</a> <a id="1419" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="1423" class="Symbol">(</a><a id="1424" href="Cubical.Foundations.BiInvEquiv.html#661" class="Function">invr≡invl</a> <a id="1434" href="Cubical.Foundations.BiInvEquiv.html#1383" class="Bound">a</a><a id="1435" class="Symbol">)))</a> <a id="1439" class="Symbol">(</a><a id="1440" href="Cubical.Foundations.BiInvEquiv.html#572" class="Field">invr-rightInv</a> <a id="1454" href="Cubical.Foundations.BiInvEquiv.html#1383" class="Bound">a</a><a id="1455" class="Symbol">)</a> <a id="1457" href="Cubical.Foundations.BiInvEquiv.html#1385" class="Bound">j</a> <a id="1459" href="Cubical.Foundations.BiInvEquiv.html#1387" class="Bound">i</a>


<a id="1463" class="Keyword">module</a> <a id="1470" href="Cubical.Foundations.BiInvEquiv.html#1470" class="Module">_</a> <a id="1472" class="Symbol">{</a><a id="1473" href="Cubical.Foundations.BiInvEquiv.html#1473" class="Bound">ℓ</a><a id="1474" class="Symbol">}</a> <a id="1476" class="Symbol">{</a><a id="1477" href="Cubical.Foundations.BiInvEquiv.html#1477" class="Bound">A</a> <a id="1479" href="Cubical.Foundations.BiInvEquiv.html#1479" class="Bound">B</a> <a id="1481" class="Symbol">:</a> <a id="1483" href="Cubical.Core.Primitives.html#957" class="Function">Type</a> <a id="1488" href="Cubical.Foundations.BiInvEquiv.html#1473" class="Bound">ℓ</a><a id="1489" class="Symbol">}</a> <a id="1491" class="Symbol">(</a><a id="1492" href="Cubical.Foundations.BiInvEquiv.html#1492" class="Bound">e</a> <a id="1494" class="Symbol">:</a> <a id="1496" href="Cubical.Foundations.BiInvEquiv.html#431" class="Record">BiInvEquiv</a> <a id="1507" href="Cubical.Foundations.BiInvEquiv.html#1477" class="Bound">A</a> <a id="1509" href="Cubical.Foundations.BiInvEquiv.html#1479" class="Bound">B</a><a id="1510" class="Symbol">)</a> <a id="1512" class="Keyword">where</a>
  <a id="1520" class="Keyword">open</a> <a id="1525" href="Cubical.Foundations.BiInvEquiv.html#431" class="Module">BiInvEquiv</a> <a id="1536" href="Cubical.Foundations.BiInvEquiv.html#1492" class="Bound">e</a>

  <a id="1541" href="Cubical.Foundations.BiInvEquiv.html#1541" class="Function">biInvEquiv→Iso-right</a> <a id="1562" class="Symbol">:</a> <a id="1564" href="Cubical.Foundations.Isomorphism.html#632" class="Record">Iso</a> <a id="1568" href="Cubical.Foundations.BiInvEquiv.html#1477" class="Bound">A</a> <a id="1570" href="Cubical.Foundations.BiInvEquiv.html#1479" class="Bound">B</a>
  <a id="1574" href="Cubical.Foundations.Isomorphism.html#726" class="Field">Iso.fun</a> <a id="1582" href="Cubical.Foundations.BiInvEquiv.html#1541" class="Function">biInvEquiv→Iso-right</a>      <a id="1608" class="Symbol">=</a> <a id="1610" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a>
  <a id="1616" href="Cubical.Foundations.Isomorphism.html#742" class="Field">Iso.inv</a> <a id="1624" href="Cubical.Foundations.BiInvEquiv.html#1541" class="Function">biInvEquiv→Iso-right</a>      <a id="1650" class="Symbol">=</a> <a id="1652" href="Cubical.Foundations.BiInvEquiv.html#555" class="Field">invr</a>
  <a id="1659" href="Cubical.Foundations.Isomorphism.html#758" class="Field">Iso.rightInv</a> <a id="1672" href="Cubical.Foundations.BiInvEquiv.html#1541" class="Function">biInvEquiv→Iso-right</a> <a id="1693" class="Symbol">=</a> <a id="1695" href="Cubical.Foundations.BiInvEquiv.html#572" class="Field">invr-rightInv</a>
  <a id="1711" href="Cubical.Foundations.Isomorphism.html#789" class="Field">Iso.leftInv</a> <a id="1723" href="Cubical.Foundations.BiInvEquiv.html#1541" class="Function">biInvEquiv→Iso-right</a>  <a id="1745" class="Symbol">=</a> <a id="1747" href="Cubical.Foundations.BiInvEquiv.html#874" class="Function">invr-leftInv</a>

  <a id="1763" href="Cubical.Foundations.BiInvEquiv.html#1763" class="Function">biInvEquiv→Iso-left</a> <a id="1783" class="Symbol">:</a> <a id="1785" href="Cubical.Foundations.Isomorphism.html#632" class="Record">Iso</a> <a id="1789" href="Cubical.Foundations.BiInvEquiv.html#1477" class="Bound">A</a> <a id="1791" href="Cubical.Foundations.BiInvEquiv.html#1479" class="Bound">B</a>
  <a id="1795" href="Cubical.Foundations.Isomorphism.html#726" class="Field">Iso.fun</a> <a id="1803" href="Cubical.Foundations.BiInvEquiv.html#1763" class="Function">biInvEquiv→Iso-left</a>      <a id="1828" class="Symbol">=</a> <a id="1830" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a>
  <a id="1836" href="Cubical.Foundations.Isomorphism.html#742" class="Field">Iso.inv</a> <a id="1844" href="Cubical.Foundations.BiInvEquiv.html#1763" class="Function">biInvEquiv→Iso-left</a>      <a id="1869" class="Symbol">=</a> <a id="1871" href="Cubical.Foundations.BiInvEquiv.html#609" class="Field">invl</a>
  <a id="1878" href="Cubical.Foundations.Isomorphism.html#758" class="Field">Iso.rightInv</a> <a id="1891" href="Cubical.Foundations.BiInvEquiv.html#1763" class="Function">biInvEquiv→Iso-left</a> <a id="1911" class="Symbol">=</a> <a id="1913" href="Cubical.Foundations.BiInvEquiv.html#1156" class="Function">invl-rightInv</a>
  <a id="1929" href="Cubical.Foundations.Isomorphism.html#789" class="Field">Iso.leftInv</a> <a id="1941" href="Cubical.Foundations.BiInvEquiv.html#1763" class="Function">biInvEquiv→Iso-left</a>  <a id="1962" class="Symbol">=</a> <a id="1964" href="Cubical.Foundations.BiInvEquiv.html#626" class="Field">invl-leftInv</a>

  <a id="1980" href="Cubical.Foundations.BiInvEquiv.html#1980" class="Function">biInvEquiv→Equiv-right</a> <a id="2003" href="Cubical.Foundations.BiInvEquiv.html#2003" class="Function">biInvEquiv→Equiv-left</a> <a id="2025" class="Symbol">:</a> <a id="2027" href="Cubical.Foundations.BiInvEquiv.html#1477" class="Bound">A</a> <a id="2029" href="Agda.Builtin.Cubical.Glue.html#1051" class="Function Operator">≃</a> <a id="2031" href="Cubical.Foundations.BiInvEquiv.html#1479" class="Bound">B</a>
  <a id="2035" href="Cubical.Foundations.BiInvEquiv.html#1980" class="Function">biInvEquiv→Equiv-right</a> <a id="2058" class="Symbol">=</a> <a id="2060" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="2064" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2066" href="Cubical.Foundations.Isomorphism.html#2362" class="Function">isoToIsEquiv</a> <a id="2079" href="Cubical.Foundations.BiInvEquiv.html#1541" class="Function">biInvEquiv→Iso-right</a>
  <a id="2102" href="Cubical.Foundations.BiInvEquiv.html#2003" class="Function">biInvEquiv→Equiv-left</a>  <a id="2125" class="Symbol">=</a> <a id="2127" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">fun</a> <a id="2131" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2133" href="Cubical.Foundations.Isomorphism.html#2362" class="Function">isoToIsEquiv</a> <a id="2146" href="Cubical.Foundations.BiInvEquiv.html#1763" class="Function">biInvEquiv→Iso-left</a>

  <a id="2169" class="Comment">-- since Iso.rightInv ends up getting modified during iso→HAEquiv, in some sense biInvEquiv→Iso-left</a>
  <a id="2272" class="Comment">--  is the most natural choice for forming a HAEquiv from a BiInvEquiv</a>
  <a id="2345" href="Cubical.Foundations.BiInvEquiv.html#2345" class="Function">biInvEquiv→HAEquiv</a> <a id="2364" class="Symbol">:</a> <a id="2366" href="Cubical.Foundations.HAEquiv.html#2303" class="Function">HAEquiv</a> <a id="2374" href="Cubical.Foundations.BiInvEquiv.html#1477" class="Bound">A</a> <a id="2376" href="Cubical.Foundations.BiInvEquiv.html#1479" class="Bound">B</a>
  <a id="2380" href="Cubical.Foundations.BiInvEquiv.html#2345" class="Function">biInvEquiv→HAEquiv</a> <a id="2399" class="Symbol">=</a> <a id="2401" href="Cubical.Foundations.HAEquiv.html#2480" class="Function">iso→HAEquiv</a> <a id="2413" href="Cubical.Foundations.BiInvEquiv.html#1763" class="Function">biInvEquiv→Iso-left</a>


<a id="2435" class="Keyword">module</a> <a id="2442" href="Cubical.Foundations.BiInvEquiv.html#2442" class="Module">_</a> <a id="2444" class="Symbol">{</a><a id="2445" href="Cubical.Foundations.BiInvEquiv.html#2445" class="Bound">ℓ</a><a id="2446" class="Symbol">}</a> <a id="2448" class="Symbol">{</a><a id="2449" href="Cubical.Foundations.BiInvEquiv.html#2449" class="Bound">A</a> <a id="2451" href="Cubical.Foundations.BiInvEquiv.html#2451" class="Bound">B</a> <a id="2453" class="Symbol">:</a> <a id="2455" href="Cubical.Core.Primitives.html#957" class="Function">Type</a> <a id="2460" href="Cubical.Foundations.BiInvEquiv.html#2445" class="Bound">ℓ</a><a id="2461" class="Symbol">}</a> <a id="2463" class="Symbol">(</a><a id="2464" href="Cubical.Foundations.BiInvEquiv.html#2464" class="Bound">i</a> <a id="2466" class="Symbol">:</a> <a id="2468" href="Cubical.Foundations.Isomorphism.html#632" class="Record">Iso</a> <a id="2472" href="Cubical.Foundations.BiInvEquiv.html#2449" class="Bound">A</a> <a id="2474" href="Cubical.Foundations.BiInvEquiv.html#2451" class="Bound">B</a><a id="2475" class="Symbol">)</a> <a id="2477" class="Keyword">where</a>
  <a id="2485" class="Keyword">open</a> <a id="2490" href="Cubical.Foundations.Isomorphism.html#632" class="Module">Iso</a> <a id="2494" href="Cubical.Foundations.BiInvEquiv.html#2464" class="Bound">i</a>

  <a id="2499" href="Cubical.Foundations.BiInvEquiv.html#2499" class="Function">iso→BiInvEquiv</a> <a id="2514" class="Symbol">:</a> <a id="2516" href="Cubical.Foundations.BiInvEquiv.html#431" class="Record">BiInvEquiv</a> <a id="2527" href="Cubical.Foundations.BiInvEquiv.html#2449" class="Bound">A</a> <a id="2529" href="Cubical.Foundations.BiInvEquiv.html#2451" class="Bound">B</a>
  <a id="2533" href="Cubical.Foundations.BiInvEquiv.html#539" class="Field">BiInvEquiv.fun</a> <a id="2548" href="Cubical.Foundations.BiInvEquiv.html#2499" class="Function">iso→BiInvEquiv</a>           <a id="2573" class="Symbol">=</a> <a id="2575" href="Cubical.Foundations.Isomorphism.html#726" class="Field">fun</a>
  <a id="2581" href="Cubical.Foundations.BiInvEquiv.html#555" class="Field">BiInvEquiv.invr</a> <a id="2597" href="Cubical.Foundations.BiInvEquiv.html#2499" class="Function">iso→BiInvEquiv</a>          <a id="2621" class="Symbol">=</a> <a id="2623" href="Cubical.Foundations.Isomorphism.html#742" class="Field">inv</a>
  <a id="2629" href="Cubical.Foundations.BiInvEquiv.html#572" class="Field">BiInvEquiv.invr-rightInv</a> <a id="2654" href="Cubical.Foundations.BiInvEquiv.html#2499" class="Function">iso→BiInvEquiv</a> <a id="2669" class="Symbol">=</a> <a id="2671" href="Cubical.Foundations.Isomorphism.html#758" class="Field">rightInv</a>
  <a id="2682" href="Cubical.Foundations.BiInvEquiv.html#609" class="Field">BiInvEquiv.invl</a> <a id="2698" href="Cubical.Foundations.BiInvEquiv.html#2499" class="Function">iso→BiInvEquiv</a>          <a id="2722" class="Symbol">=</a> <a id="2724" href="Cubical.Foundations.Isomorphism.html#742" class="Field">inv</a>
  <a id="2730" href="Cubical.Foundations.BiInvEquiv.html#626" class="Field">BiInvEquiv.invl-leftInv</a> <a id="2754" href="Cubical.Foundations.BiInvEquiv.html#2499" class="Function">iso→BiInvEquiv</a>  <a id="2770" class="Symbol">=</a> <a id="2772" href="Cubical.Foundations.Isomorphism.html#789" class="Field">leftInv</a>

</pre></body></html>